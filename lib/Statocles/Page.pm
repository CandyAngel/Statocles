package Statocles::Page;
# ABSTRACT: Render documents into HTML

use Statocles::Role;
use Text::Markdown;
use Text::Template;

requires 'render';

=attr path

The absolute URL path to save this page to.

=cut

has path => (
    is => 'ro',
    isa => Str,
);

=attr markdown

The L<Text::Markdown> object to render document Markdown.

=cut

has markdown => (
    is => 'ro',
    isa => InstanceOf['Text::Markdown'],
    default => sub { Text::Markdown->new },
);

my @template_attrs = (
    is => 'ro',
    isa => InstanceOf['Text::Template'],
    coerce => sub {
        die "Template is undef" unless defined $_[0];
        return !ref $_[0] 
            ? Text::Template->new( TYPE => 'STRING', SOURCE => $_[0] )
            : $_[0]
            ;
    },
);

=attr template

The main template for this page. The result will be wrapped in the C<layout>
template.

=cut

has template => @template_attrs;

=attr layout

The layout template for this page, which will wrap the content generated by the
C<template>.

=cut

has layout => (
    @template_attrs,
    default => sub {
        Text::Template->new( type => 'STRING', source => '{$content}' ),
    },
);

1;
__END__

=head1 DESCRIPTION

A Statocles::Page takes one or more documents and renders them into one or more
HTML pages.

=head1 SEE ALSO

=over

=item L<Statocles::Page::Document>

A page that renders a single document.

=item L<Statocles::Page::List>

A page that renders a list of other pages.

=back

