<!DOCTYPE html>
<html>
    <head>
        <link href="/Statocles/theme/css/normalize.css" rel="stylesheet">
        <link href="/Statocles/theme/css/skeleton.css" rel="stylesheet">
        <link href="/Statocles/theme/css/statocles-default.css" rel="stylesheet">
        <title>Statocles</title>
        <link href="http://fonts.googleapis.com/css?family=Forum" rel="stylesheet" type="text/css">
<style>
    h1, h2, h3, h4, h5, h6 {
        font-family: 'Forum', sans-serif;
        font-weight: bold;
    }

    #index-banner {
        font-family: 'Forum', sans-serif;
        height: 300px;
        position: relative;
        margin-bottom: 1em;
        border-bottom: 1px solid #777
    }
    #index-banner:before, #index-banner:after {
        width: 60px;
        height: 300px;
        content: url(/Statocles/theme/images/column.png);
    }
    #index-banner:before {
        position: absolute;
        top: 0;
        left: 0;
    }
    #index-banner:after {
        position: absolute;
        top: 0;
        right: 0;
    }

    #index-banner h1 {
        padding-top: 1em;
        text-transform: uppercase;
        font-size: 72px;
        text-align: center;
    }
    #index-banner h1 small {
        display: block;
        font-size: 24px;
    }

    #index-banner .latest-release {
        text-align: center;
    }

</style>

    </head>
    <body>
        <header>
            <nav class="navbar">
                <div class="container">
                    <a class="brand" href="/Statocles/">Statocles</a>
                    <ul>
                        <li>
                            <a href="/Statocles/blog">Blog</a>
                        </li>
                        <li>
                            <a href="/Statocles/pod">Docs</a>
                        </li>
                        <li>
                            <a href="http://github.com/preaction/Statocles">Code</a>
                        </li>
                        <li>
                            <a href="http://github.com/preaction/Statocles/issues">Bugs</a>
                        </li>
                        <li>
                            <a href="http://metacpan.org/pod/Statocles">CPAN</a>
                        </li>
                        <li>
                            <a href="https://chat.mibbit.com/?channel=%23statocles&amp;server=irc.perl.org">IRC</a>
                        </li>
                    </ul>
                </div>
            </nav>
            <a href="https://github.com/preaction/Statocles"><img alt="Fork me on GitHub" data-canonical-src="https://s3.amazonaws.com/github/ribbons/forkme_right_red_aa0000.png" src="https://camo.githubusercontent.com/365986a132ccd6a44c23a9169022c0b5c890c387/68747470733a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f7265645f6161303030302e706e67" style="position: absolute; top: 0; right: 0; z-index: 1000; border: 0;"></a>

        </header>
        <div class="main container">
            <pre>package Statocles::Site;
# ABSTRACT: An entire, configured website

use Statocles::Base &#39;Class&#39;, &#39;Emitter&#39;;
use Scalar::Util qw( blessed );
use Text::Markdown;
use Mojo::URL;
use Mojo::DOM;
use Mojo::Log;
use Statocles::Page::Plain;
use Statocles::Page::File;

=attr title

The site title, used in templates.

=cut

has title =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; Str,
);

=attr base_url

The base URL of the site, including protocol and domain. Used mostly for feeds.

This can be overridden by L&lt;base_url in Deploy|Statocles::Deploy/base_url&gt;.

=cut

has base_url =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; Str,
    default =&gt; sub { &#39;/&#39; },
);

=attr theme

The L&lt;theme|Statocles::Theme&gt; for this site. All apps share the same theme.

=cut

has theme =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; Theme,
    coerce =&gt; Theme-&gt;coercion,
    default =&gt; sub {
        Statocles::Theme-&gt;new( store =&gt; &#39;::default&#39; );
    },
);

=attr apps

The applications in this site. Each application has a name
that can be used later.

=cut

has apps =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; HashRef[ConsumerOf[&#39;Statocles::App&#39;]],
    default =&gt; sub { {} },
);

=attr index

The application to use as the site index. The application&#39;s individual index()
method will be called to get the index page.

=cut

has index =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; Str,
    default =&gt; sub { &#39;&#39; },
);

=attr nav

Named navigation lists. A hash of arrays of hashes with the following keys:

    title - The title of the link
    href - The href of the link

The most likely name for your navigation will be C&lt;main&gt;. Navigation names
are defined by your L&lt;theme|Statocles::Theme&gt;. For example:

    {
        main =&gt; [
            {
                title =&gt; &#39;Blog&#39;,
                href =&gt; &#39;/blog&#39;,
            },
            {
                title =&gt; &#39;Contact&#39;,
                href =&gt; &#39;/contact.html&#39;,
            },
        ],
    }

=cut

has _nav =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; LinkHash,
    coerce =&gt; LinkHash-&gt;coercion,
    default =&gt; sub { {} },
    init_arg =&gt; &#39;nav&#39;,
);

=attr build_store

The L&lt;store|Statocles::Store&gt; object to use for C&lt;build()&gt;. This is a workspace
and will be rebuilt often, using the C&lt;build&gt; and C&lt;daemon&gt; commands. This is
also the store the C&lt;daemon&gt; command reads to serve the site.

=cut

has build_store =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; Store,
    default =&gt; sub {
        my $path = Path::Tiny-&gt;new( &#39;.statocles&#39;, &#39;build&#39; );
        if ( !$path-&gt;is_dir ) {
            # Automatically make the build directory
            $path-&gt;mkpath;
        }
        return Store-&gt;coercion-&gt;( $path );
    },
    coerce =&gt; sub {
        my ( $arg ) = @_;
        if ( !ref $arg &amp;&amp; !-d $arg ) {
            # Automatically make the build directory
            Path::Tiny-&gt;new( $arg )-&gt;mkpath;
        }
        return Store-&gt;coercion-&gt;( $arg );
    },
);

=attr deploy

The L&lt;deploy object|Statocles::Deploy&gt; to use for C&lt;deploy()&gt;. This is
intended to be the production deployment of the site. A build gets promoted to
production by using the C&lt;deploy&gt; command.

=cut

has _deploy =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; ConsumerOf[&#39;Statocles::Deploy&#39;],
    required =&gt; 1,
    init_arg =&gt; &#39;deploy&#39;,
    coerce =&gt; sub {
        if ( ( blessed $_[0] &amp;&amp; $_[0]-&gt;isa( &#39;Path::Tiny&#39; ) ) || !ref $_[0] ) {
            require Statocles::Deploy::File;
            return Statocles::Deploy::File-&gt;new(
                path =&gt; $_[0],
            );
        }
        return $_[0];
    },
);

=attr data

A hash of arbitrary data available to theme templates. This is a good place to
put extra structured data like social network links or make easy customizations
to themes like header image URLs.

=cut

has data =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; HashRef,
    default =&gt; sub { {} },
);

=attr log

A L&lt;Mojo::Log&gt; object to write logs to. Defaults to STDERR.

=cut

has log =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; InstanceOf[&#39;Mojo::Log&#39;],
    lazy =&gt; 1,
    default =&gt; sub {
        Mojo::Log-&gt;new( level =&gt; &#39;warn&#39; );
    },
);

=attr markdown

The Text::Markdown object to use to turn Markdown into HTML. Defaults to a
plain Text::Markdown object.

Any object with a &quot;markdown&quot; method will work here.

=cut

has markdown =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; HasMethods[&#39;markdown&#39;],
    default =&gt; sub { Text::Markdown-&gt;new },
);

# The current deploy we&#39;re writing to
has _write_deploy =&gt; (
    is =&gt; &#39;rw&#39;,
    isa =&gt; ConsumerOf[&#39;Statocles::Deploy&#39;],
    clearer =&gt; &#39;_clear_write_deploy&#39;,
);

=method BUILD

Register this site as the global site.

=cut

sub BUILD {
    my ( $self ) = @_;

    if ( $self-&gt;index &amp;&amp; !$self-&gt;app( $self-&gt;index ) ) {
        die sprintf &#39;ERROR: Index app &quot;%s&quot; does not exist&#39;, $self-&gt;index;
    }

    $Statocles::SITE = $self;
    for my $app ( values %{ $self-&gt;apps } ) {
        $app-&gt;site( $self );
    }
}

=method app( name )

Get the app with the given C&lt;name&gt;.

=cut

sub app {
    my ( $self, $name ) = @_;
    return $self-&gt;apps-&gt;{ $name };
}

=method nav( name )

Get the list of links for the given nav. Each link is a L&lt;Statocles::Link&gt; object.

    title - The title of the link
    href - The href of the link

If the named nav does not exist, returns an empty list.

=cut

sub nav {
    my ( $self, $name ) = @_;
    return $self-&gt;_nav-&gt;{ $name } ? @{ $self-&gt;_nav-&gt;{ $name } } : ();
}

=method build

Build the site in its build location.

=cut

our %PAGE_PRIORITY = (
    &#39;Statocles::Page::File&#39; =&gt; -100,
);

sub build {
    my ( $self ) = @_;

    my $store = $self-&gt;build_store;

    # Remove all pages from the build directory first
    $_-&gt;remove_tree for $store-&gt;path-&gt;children;

    my $apps = $self-&gt;apps;
    my @pages;
    my %seen_paths;
    my %args = (
        site =&gt; $self,
    );

    # Collect all the pages for this site
    for my $app_name ( keys %{ $apps } ) {
        my $app = $apps-&gt;{$app_name};

        my @app_pages = $app-&gt;pages;
        if ( $self-&gt;index eq $app_name ) {

            die sprintf &#39;ERROR: Index app &quot;%s&quot; did not generate any pages&#39; . &quot;\n&quot;, $self-&gt;index
                unless @app_pages;

            # Rename the app&#39;s page so that we don&#39;t get two pages with identical
            # content, which is bad for SEO
            $app_pages[0]-&gt;path( &#39;/index.html&#39; );
        }

        for my $page ( @app_pages ) {
            $seen_paths{ $page-&gt;path }{ $app_name } = $page;
        }
    }

    for my $path ( keys %seen_paths ) {
        my %seen_apps = %{ $seen_paths{$path} };
        # Warn about pages generated by more than one app
        if ( keys %seen_apps &gt; 1 ) {
            my @seen_app_names = map { $_-&gt;[0] }
                            sort { $b-&gt;[1] &lt;=&gt; $a-&gt;[1] }
                            map { [ $_, $PAGE_PRIORITY{ ref $seen_apps{ $_ } } || 0 ] }
                            keys %seen_apps
                            ;

            $self-&gt;log-&gt;warn(
                sprintf &#39;Duplicate page &quot;%s&quot; from apps: %s. Using %s&#39;,
                    $path,
                    join( &quot;, &quot;, @seen_app_names ),
                    $seen_app_names[0],
            );

            push @pages, $seen_apps{ $seen_app_names[0] };
        }
        else {
           push @pages, values %seen_apps;
        }
    }

    $self-&gt;emit(
        &#39;before_build_write&#39;,
        class =&gt; &#39;Statocles::Event::Pages&#39;,
        pages =&gt; \@pages,
    );

    # Rewrite page content to add base URL
    my $base_url = $self-&gt;base_url;
    if ( $self-&gt;_write_deploy ) {
        $base_url = $self-&gt;_write_deploy-&gt;base_url || $base_url;
    }
    my $base_path = Mojo::URL-&gt;new( $base_url )-&gt;path;
    $base_path =~ s{/$}{};

    for my $page ( @pages ) {
        my $content = $page-&gt;render( %args );

        if ( !ref $content ) {
            if ( $base_path =~ /\S/ ) {
                my $dom = Mojo::DOM-&gt;new( $content );
                for my $attr ( qw( src href ) ) {
                    for my $el ( $dom-&gt;find( &quot;[$attr]&quot; )-&gt;each ) {
                        my $url = $el-&gt;attr( $attr );
                        next unless $url =~ m{^/};
                        $el-&gt;attr( $attr, join &quot;&quot;, $base_path, $url );
                    }
                }
                $content = $dom-&gt;to_string;
            }
        }

        $store-&gt;write_file( $page-&gt;path, $content );
    }

    # Build the sitemap.xml
    # html files only
    my @indexed_pages = grep { $_-&gt;path =~ /[.]html?$/ } @pages;
    my $tmpl = $self-&gt;theme-&gt;template( site =&gt; &#39;sitemap.xml&#39; );
    my $sitemap = Statocles::Page::Plain-&gt;new(
        path =&gt; &#39;/sitemap.xml&#39;,
        content =&gt; $tmpl-&gt;render( site =&gt; $self, pages =&gt; \@indexed_pages ),
    );
    push @pages, $sitemap;
    $store-&gt;write_file( &#39;sitemap.xml&#39;, $sitemap-&gt;render );

    # robots.txt is the best way for crawlers to automatically discover sitemap.xml
    # We should do more with this later...
    my $robots_tmpl = $self-&gt;theme-&gt;template( site =&gt; &#39;robots.txt&#39; );
    my $robots = Statocles::Page::Plain-&gt;new(
        path =&gt; &#39;/robots.txt&#39;,
        content =&gt; $robots_tmpl-&gt;render( site =&gt; $self ),
    );
    push @pages, $robots;
    $store-&gt;write_file( &#39;robots.txt&#39;, $robots-&gt;render );

    # Add the theme
    my $theme_iter = $self-&gt;theme-&gt;store-&gt;find_files();
    while ( my $theme_file = $theme_iter-&gt;() ) {
        my $fh = $self-&gt;theme-&gt;store-&gt;open_file( $theme_file );
        push @pages, Statocles::Page::File-&gt;new(
            path =&gt; join( &#39;/&#39;, &#39;&#39;, &#39;theme&#39;, $theme_file ),
            fh =&gt; $fh,
        );
        $store-&gt;write_file( Path::Tiny-&gt;new( &#39;theme&#39;, $theme_file ), $fh );
    }

    $self-&gt;emit( build =&gt; class =&gt; &#39;Statocles::Event::Pages&#39;, pages =&gt; \@pages );

    return;
}

=method deploy

Deploy the site to its destination.

=cut

sub deploy {
    my ( $self ) = @_;
    $self-&gt;_write_deploy( $self-&gt;_deploy );
    $self-&gt;build;
    $self-&gt;_deploy-&gt;deploy( $self-&gt;build_store );
    $self-&gt;_clear_write_deploy;
}

=method url( path )

Get the full URL to the given path by prepending the C&lt;base_url&gt;.

=cut

sub url {
    my ( $self, $path ) = @_;
    my $base    = $self-&gt;_write_deploy &amp;&amp; $self-&gt;_write_deploy-&gt;base_url
                ? $self-&gt;_write_deploy-&gt;base_url
                : $self-&gt;base_url;

    # Remove index.html from the end of the path, since it&#39;s redundant
    $path =~ s{/index[.]html$}{};

    # Remove the / from both sides of the join so we don&#39;t double up
    $base =~ s{/$}{};
    $path =~ s{^/}{};

    return join &quot;/&quot;, $base, ( $path || !$base ? ( $path ) : () );
}

1;
__END__

=head1 SYNOPSIS

    my $site = Statocles::Site-&gt;new(
        title =&gt; &#39;My Site&#39;,
        nav =&gt; [
            { title =&gt; &#39;Home&#39;, href =&gt; &#39;/&#39; },
            { title =&gt; &#39;Blog&#39;, href =&gt; &#39;/blog&#39; },
        ],
        apps =&gt; {
            blog =&gt; Statocles::App::Blog-&gt;new( ... ),
        },
    );

    $site-&gt;deploy;

=head1 DESCRIPTION

A Statocles::Site is a collection of L&lt;applications|Statocles::App&gt;.

=head1 EVENTS

The site object exposes the following events.

=head2 before_build_write

This event is fired after the pages have been built by the apps, but before
any page is written to the C&lt;build_store&gt;.

You can use this event to add new pages or edit the pages already created.

The event will be a
L&lt;Statocles::Event::Pages|Statocles::Event/Statocles::Event::Pages&gt; object
containing all the pages built by the apps.

=head2 build

This event is fired after the site has been built and the pages written to the
C&lt;build_store&gt;.

The event will be a
L&lt;Statocles::Event::Pages|Statocles::Event/Statocles::Event::Pages&gt; object
containing all the pages built by the site.

</pre>

        </div>
        <footer>
            <script>
  (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
  })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

  ga('create', 'UA-61295159-3', 'auto');
  ga('send', 'pageview');

</script>

            <div class="container tagline">
                <a href="http://preaction.github.io/Statocles">Made with Statocles</a><br>
                <a href="http://www.perl.org">Powered by Perl</a>
            </div>
        </footer>
    </body>
</html>
