<!DOCTYPE html>
<html>
    <head>
        <link href="/Statocles/theme/css/normalize.css" rel="stylesheet">
        <link href="/Statocles/theme/css/skeleton.css" rel="stylesheet">
        <link href="/Statocles/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <title>Statocles</title>
        <link href="http://fonts.googleapis.com/css?family=Forum" rel="stylesheet" type="text/css">
<style>
    h1, h2, h3, h4, h5, h6 {
        font-family: 'Forum', sans-serif;
        font-weight: bold;
    }

    #index-banner {
        font-family: 'Forum', sans-serif;
        height: 300px;
        position: relative;
        margin-bottom: 1em;
        border-bottom: 1px solid #777
    }
    #index-banner:before, #index-banner:after {
        width: 60px;
        height: 300px;
        content: url(/Statocles/theme/images/column.png);
    }
    #index-banner:before {
        position: absolute;
        top: 0;
        left: 0;
    }
    #index-banner:after {
        position: absolute;
        top: 0;
        right: 0;
    }

    #index-banner h1 {
        padding-top: 1em;
        text-transform: uppercase;
        font-size: 72px;
        text-align: center;
    }
    #index-banner h1 small {
        display: block;
        font-size: 24px;
    }

    #index-banner .latest-release {
        text-align: center;
    }

</style>

    </head>
    <body>
        <header>
            <nav class="navbar">
                <div class="container">
                    <a class="brand" href="/Statocles/">Statocles</a>
                    <ul>
                        <li>
                            <a href="/Statocles/blog">Blog</a>
                        </li>
                        <li>
                            <a href="/Statocles/pod">Docs</a>
                        </li>
                        <li>
                            <a href="/Statocles/gallery">Gallery</a>
                        </li>
                        <li>
                            <a href="http://github.com/preaction/Statocles">Code</a>
                        </li>
                        <li>
                            <a href="http://github.com/preaction/Statocles/issues">Bugs</a>
                        </li>
                        <li>
                            <a href="http://metacpan.org/pod/Statocles">CPAN</a>
                        </li>
                        <li>
                            <a href="https://chat.mibbit.com/?channel=%23statocles&amp;server=irc.perl.org">IRC</a>
                        </li>
                    </ul>
                    
                </div>
            </nav>
            <a href="https://github.com/preaction/Statocles"><img alt="Fork me on GitHub" data-canonical-src="https://s3.amazonaws.com/github/ribbons/forkme_right_red_aa0000.png" src="https://camo.githubusercontent.com/365986a132ccd6a44c23a9169022c0b5c890c387/68747470733a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f7265645f6161303030302e706e67" style="position: absolute; top: 0; right: 0; z-index: 1000; border: 0;"></a>

        </header>
        <div class="main container">
            <a class="button" href="/Statocles/pod/Statocles/App/Blog.html">
    Back to documentation
</a>
<pre>package Statocles::App::Blog;
# ABSTRACT: A blog application

use Statocles::Base &#39;Class&#39;;
use Getopt::Long qw( GetOptionsFromArray );
use Statocles::Store::File;
use Statocles::Page::Document;
use Statocles::Page::List;

with &#39;Statocles::App&#39;;

=attr store

The L&lt;store|Statocles::Store&gt; to read for documents.

=cut

has store =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; Store,
    coerce =&gt; Store-&gt;coercion,
    required =&gt; 1,
);

=attr tag_text

A hash of tag and descriptions to introduce tags. Having a description is optional.

=cut

has tag_text =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; HashRef,
    default =&gt; sub { {} },
);

=attr page_size

The number of posts to put in a page (the main page and the tag pages). Defaults
to 5.

=cut

has page_size =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; Int,
    default =&gt; sub { 5 },
);

=attr index_tags

Filter the tags shown in the index page. An array of tags prefixed with either
a + or a -. By prefixing the tag with a &quot;-&quot;, it will be removed from the index,
unless a later tag prefixed with a &quot;+&quot; also matches.

By default, all tags are shown on the index page.

So, given a document with tags &quot;foo&quot;, and &quot;bar&quot;:

    index_tags =&gt; [ ];                  # document will be included
    index_tags =&gt; [ &#39;-foo&#39; ];           # document will not be included
    index_tags =&gt; [ &#39;-foo&#39;, &#39;+bar&#39; ];   # document will be included

=cut

has index_tags =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; ArrayRef[Str],
    default =&gt; sub { [] },
);

# A cache of the last set of post pages we have
# XXX: We need to allow apps to have a &quot;clear&quot; the way that Store and Theme do
has _post_pages =&gt; (
    is =&gt; &#39;rw&#39;,
    isa =&gt; ArrayRef,
    default =&gt; sub { [] },
);

=method command

    my $exitval = $app-&gt;command( $app_name, @args );

Run a command on this app. The app name is used to build the help, so
users get exactly what they need to run.

=cut

our $default_post = {
    tags =&gt; undef,
    content =&gt; &lt;&lt;&#39;ENDCONTENT&#39;,
Markdown content goes here.
ENDCONTENT
};

my $USAGE_INFO = &lt;&lt;&#39;ENDHELP&#39;;
Usage:
    $name help -- This help file
    $name post [--date YYYY-MM-DD] &lt;title&gt; -- Create a new blog post with the given title
ENDHELP

sub command {
    my ( $self, $name, @argv ) = @_;

    if ( !$argv[0] ) {
        say STDERR &quot;ERROR: Missing command&quot;;
        say STDERR eval &quot;qq{$USAGE_INFO}&quot;;
        return 1;
    }

    if ( $argv[0] eq &#39;help&#39; ) {
        say eval &quot;qq{$USAGE_INFO}&quot;;
    }
    elsif ( $argv[0] eq &#39;post&#39; ) {
        my %opt;
        GetOptionsFromArray( \@argv, \%opt,
            &#39;date:s&#39;,
        );

        my %doc = (
            %$default_post,
            title =&gt; join &quot; &quot;, @argv[1..$#argv],
        );

        # Read post content on STDIN
        if ( !-t *STDIN ) {
            my $content = do { local $/; &lt;STDIN&gt; };
            %doc = (
                %doc,
                $self-&gt;store-&gt;parse_frontmatter( &quot;&lt;STDIN&gt;&quot;, $content ),
            );

            # Re-open STDIN as the TTY so that the editor (vim) can use it
            # XXX Is this also a problem on Windows?
            if ( -e &#39;/dev/tty&#39; ) {
                close STDIN;
                open STDIN, &#39;/dev/tty&#39;;
            }
        }

        if ( !$ENV{EDITOR} &amp;&amp; !$doc{title} ) {
            say STDERR &lt;&lt;&quot;ENDHELP&quot;;
Title is required when \$EDITOR is not set.

Usage: $name post &lt;title&gt;
ENDHELP
            return 1;
        }

        my ( $year, $mon, $day );
        if ( $opt{ date } ) {
            ( $year, $mon, $day ) = split /-/, $opt{date};
        }
        else {
            ( undef, undef, undef, $day, $mon, $year ) = localtime;
            $year += 1900;
            $mon += 1;
        }

        my @date_parts = (
            sprintf( &#39;%04i&#39;, $year ),
            sprintf( &#39;%02i&#39;, $mon ),
            sprintf( &#39;%02i&#39;, $day ),
        );

        if ( $ENV{EDITOR} ) {
            # I can see no good way to test this automatically
            my $slug = $self-&gt;make_slug( $doc{title} || &quot;new post&quot; );
            my $path = Path::Tiny-&gt;new( @date_parts, $slug, &quot;index.markdown&quot; );
            my $tmp_path = $self-&gt;store-&gt;write_document( $path =&gt; \%doc );
            system $ENV{EDITOR}, $tmp_path;
            %doc = %{ $self-&gt;store-&gt;read_document( $path ) };
            $self-&gt;store-&gt;path-&gt;child( $path )-&gt;remove;
        }

        my $slug = $self-&gt;make_slug( $doc{title} );
        my $path = Path::Tiny-&gt;new( @date_parts, $slug, &quot;index.markdown&quot; );
        my $full_path = $self-&gt;store-&gt;write_document( $path =&gt; \%doc );
        say &quot;New post at: $full_path&quot;;

    }
    else {
        say STDERR qq{ERROR: Unknown command &quot;$argv[0]&quot;};
        say STDERR eval &quot;qq{$USAGE_INFO}&quot;;
        return 1;
    }

    return 0;
}

=method make_slug

    my $slug = $app-&gt;make_slug( $title );

Given a post title, remove special characters to create a slug.

=cut

sub make_slug {
    my ( $self, $slug ) = @_;
    $slug =~ s/[\W]+/-/g;
    return lc $slug;
}

=method post_pages

    my @pages = $app-&gt;post_pages;

Get the individual post L&lt;Statocles::Page&gt; objects.

=cut

sub post_pages {
    my ( $self ) = @_;
    my @pages = map { $self-&gt;_make_post_page( $_ ) } $self-&gt;_sorted_docs;
    $self-&gt;_post_pages( [ @pages ] );
    return @pages;
}

=method post_files

    my @pages = $app-&gt;post_files;

Get all the post collateral files as L&lt;Statocles::Page&gt; objects. Markdown
files will be created as L&lt;Statocles::Page::Document&gt; objects, any other files
will be L&lt;Statocles::Page::File&gt; objects.

=cut

sub post_files {
    my ( $self ) = @_;
    my @pages;

    my @paths;
    my $iter = $self-&gt;store-&gt;find_files( include_documents =&gt; 1 );
    while ( my $path = $iter-&gt;() ) {
        push @paths, $path;
    }

    for my $path ( @paths ) {
        # Files must be in folders
        next unless $path =~ m{^/(\d{4})/(\d{2})/(\d{2})/[^/]+/};

        # index.markdown is the post itself
        next if $path =~ m{/index[.]markdown$};
        next if $path =~ m{/index[.]html$};

        if ( $self-&gt;store-&gt;is_document( $path ) ) {
            my $page_path = $path;
            $page_path =~ s{[.]\w+$}{.html};

            my %args = (
                path =&gt; $page_path,
                app =&gt; $self,
                layout =&gt; $self-&gt;site-&gt;theme-&gt;template( site =&gt; &#39;layout.html&#39; ),
                document =&gt; $self-&gt;store-&gt;read_document( $path ),
            );

            push @pages, Statocles::Page::Document-&gt;new( %args );
        }
        else {
            # If there&#39;s a markdown file, don&#39;t keep the html file
            if ( $path =~ /[.]html$/ ) {
                my $doc_path = &quot;$path&quot;;
                $doc_path =~ s/[.]html$/.markdown/;
                next if grep { $_ eq $doc_path } @paths;
            }

            push @pages, Statocles::Page::File-&gt;new(
                path =&gt; $path,
                file_path =&gt; $self-&gt;store-&gt;path-&gt;child( $path ),
            );
        }
    }

    return @pages;
}

# Return the post docs sorted by date, pruning any docs that are after
# the current date
sub _sorted_docs {
    my ( $self ) = @_;

    my $today = Time::Piece-&gt;new-&gt;ymd;
    my @doc_dates;
    for my $doc ( @{ $self-&gt;store-&gt;documents } ) {
        my @date_parts = $doc-&gt;path =~ m{/(\d{4})/(\d{2})/(\d{2})/[^/]+(?:/index[.]markdown)?$};
        next unless @date_parts;
        my $date = join &quot;-&quot;, @date_parts;

        next if $date gt $today;

        push @doc_dates, [ $doc, $date ];
    }

    return map { $_-&gt;[0] } sort { $b-&gt;[1] cmp $a-&gt;[1] } @doc_dates;
}

sub _make_post_page {
    my ( $self, $doc ) = @_;

    my $path = $doc-&gt;path;
    $path =~ s{/{2,}}{/}g;
    $path =~ s{[.]\w+$}{.html};

    my @date_parts = $doc-&gt;path =~ m{/(\d{4})/(\d{2})/(\d{2})/[^/]+(?:/index[.]markdown)?$};
    my $date = join &quot;-&quot;, @date_parts;

    my @tags;
    for my $tag ( @{ $doc-&gt;tags } ) {
        push @tags, $self-&gt;link(
            text =&gt; $tag,
            href =&gt; join( &quot;/&quot;, &#39;tag&#39;, $self-&gt;_tag_url( $tag ), &#39;&#39; ),
        );
    }

    return Statocles::Page::Document-&gt;new(
        app =&gt; $self,
        layout =&gt; $self-&gt;site-&gt;theme-&gt;template( site =&gt; &#39;layout.html&#39; ),
        template =&gt; $self-&gt;site-&gt;theme-&gt;template( blog =&gt; &#39;post.html&#39; ),
        document =&gt; $doc,
        path =&gt; $path,
        date =&gt; $doc-&gt;has_date ? $doc-&gt;date : Time::Piece-&gt;strptime( $date, &#39;%Y-%m-%d&#39; ),
        tags =&gt; \@tags,
    );
}

=method index

    my @pages = $app-&gt;index( @post_pages );

Build the index page (a L&lt;list page|Statocles::Page::List&gt;) and all related feed pages
out of the given post pages (built from L&lt;the post_pages method|/post_pages&gt;).

=cut

my %FEEDS = (
    rss =&gt; {
        text =&gt; &#39;RSS&#39;,
        template =&gt; &#39;index.rss&#39;,
    },
    atom =&gt; {
        text =&gt; &#39;Atom&#39;,
        template =&gt; &#39;index.atom&#39;,
    },
);

sub index {
    my ( $self, @all_post_pages ) = @_;

    # Filter the index_tags
    my @index_post_pages;
    PAGE: for my $page ( @all_post_pages ) {
        my $add = 1;
        for my $tag_spec ( @{ $self-&gt;index_tags } ) {
            my $flag = substr $tag_spec, 0, 1;
            my $tag = substr $tag_spec, 1;
            if ( grep { $_ eq $tag } @{ $page-&gt;document-&gt;tags } ) {
                $add = $flag eq &#39;-&#39; ? 0 : 1;
            }
        }
        push @index_post_pages, $page if $add;
    }

    my @pages = Statocles::Page::List-&gt;paginate(
        after =&gt; $self-&gt;page_size,
        path =&gt; &#39;/page/%i/index.html&#39;,
        index =&gt; &#39;/index.html&#39;,
        # Sorting by path just happens to also sort by date
        pages =&gt; [ sort { $b-&gt;path cmp $a-&gt;path } @index_post_pages ],
        app =&gt; $self,
        template =&gt; $self-&gt;site-&gt;theme-&gt;template( blog =&gt; &#39;index.html&#39; ),
        layout =&gt; $self-&gt;site-&gt;theme-&gt;template( site =&gt; &#39;layout.html&#39; ),
    );

    return unless @pages; # Only build feeds if we have pages

    my $index = $pages[0];
    my @feed_pages;
    my @feed_links;
    for my $feed ( sort keys %FEEDS ) {
        my $page = Statocles::Page::List-&gt;new(
            app =&gt; $self,
            pages =&gt; $index-&gt;pages,
            path =&gt; &#39;index.&#39; . $feed,
            template =&gt; $self-&gt;site-&gt;theme-&gt;template( blog =&gt; $FEEDS{$feed}{template} ),
            links =&gt; {
                alternate =&gt; [
                    $self-&gt;link(
                        href =&gt; $index-&gt;path,
                        title =&gt; &#39;index&#39;,
                        type =&gt; $index-&gt;type,
                    ),
                ],
            },
        );

        push @feed_pages, $page;
        push @feed_links, $self-&gt;link(
            text =&gt; $FEEDS{ $feed }{ text },
            href =&gt; $page-&gt;path-&gt;stringify,
            type =&gt; $page-&gt;type,
        );
    }

    # Add the feeds to all the pages
    for my $page ( @pages ) {
        $page-&gt;_links-&gt;{feed} = \@feed_links;
    }

    return ( @pages, @feed_pages );
}

=method tag_pages

    my @pages = $app-&gt;tag_pages( @post_pages );

Get L&lt;pages|Statocles::Page&gt; for the tags in the given blog post documents
(build from L&lt;the post_pages method|/post_pages&gt;, including relevant feed
pages.

=cut

sub tag_pages {
    my ( $self, @post_pages ) = @_;

    my %tagged_docs = $self-&gt;_tag_docs( @post_pages );

    my @pages;
    for my $tag ( keys %tagged_docs ) {
        my @tag_pages = Statocles::Page::List-&gt;paginate(
            after =&gt; $self-&gt;page_size,
            path =&gt; join( &quot;/&quot;, &#39;&#39;, &#39;tag&#39;, $self-&gt;_tag_url( $tag ), &#39;page/%i/index.html&#39; ),
            index =&gt; join( &quot;/&quot;, &#39;&#39;, &#39;tag&#39;, $self-&gt;_tag_url( $tag ), &#39;index.html&#39; ),
            # Sorting by path just happens to also sort by date
            pages =&gt; [ sort { $b-&gt;path cmp $a-&gt;path } @{ $tagged_docs{ $tag } } ],
            app =&gt; $self,
            template =&gt; $self-&gt;site-&gt;theme-&gt;template( blog =&gt; &#39;index.html&#39; ),
            layout =&gt; $self-&gt;site-&gt;theme-&gt;template( site =&gt; &#39;layout.html&#39; ),
            data =&gt; {
                tag_text =&gt; $self-&gt;tag_text-&gt;{ $tag },
            },
        );

        my $index = $tag_pages[0];
        my @feed_pages;
        my @feed_links;
        for my $feed ( sort keys %FEEDS ) {
            my $tag_file = $self-&gt;_tag_url( $tag ) . &#39;.&#39; . $feed;

            my $page = Statocles::Page::List-&gt;new(
                app =&gt; $self,
                pages =&gt; $index-&gt;pages,
                path =&gt; join( &quot;/&quot;, &#39;tag&#39;, $tag_file ),
                template =&gt; $self-&gt;site-&gt;theme-&gt;template( blog =&gt; $FEEDS{$feed}{template} ),
                links =&gt; {
                    alternate =&gt; [
                        $self-&gt;link(
                            href =&gt; $index-&gt;path,
                            title =&gt; $tag,
                            type =&gt; $index-&gt;type,
                        ),
                    ],
                },
            );

            push @feed_pages, $page;
            push @feed_links, $self-&gt;link(
                text =&gt; $FEEDS{ $feed }{ text },
                href =&gt; $page-&gt;path-&gt;stringify,
                type =&gt; $page-&gt;type,
            );
        }

        # Add the feeds to all the pages
        for my $page ( @tag_pages ) {
            $page-&gt;_links-&gt;{feed} = \@feed_links;
        }

        push @pages, @tag_pages, @feed_pages;
    }

    return @pages;
}

=method pages

    my @pages = $app-&gt;pages;

Get all the L&lt;pages|Statocles::Page&gt; for this application.

=cut

sub pages {
    my ( $self ) = @_;
    my @post_pages = $self-&gt;post_pages;
    return (
        ( map { $self-&gt;$_( @post_pages ) } qw( index tag_pages ) ),
        $self-&gt;post_files,
        @post_pages,
    );
}

=method tags

    my @links = $app-&gt;tags;

Get a set of L&lt;link objects|Statocles::Link&gt; suitable for creating a list of
tag links. The common attributes are:

    text =&gt; &#39;The tag text&#39;
    href =&gt; &#39;The URL to the tag page&#39;

=cut

sub tags {
    my ( $self ) = @_;
    my %tagged_docs = $self-&gt;_tag_docs( @{ $self-&gt;_post_pages } );
    return map {; $self-&gt;link( text =&gt; $_, href =&gt; join( &quot;/&quot;, &#39;tag&#39;, $self-&gt;_tag_url( $_ ), &#39;&#39; ) ) }
        sort keys %tagged_docs
}

sub _tag_docs {
    my ( $self, @post_pages ) = @_;
    my %tagged_docs;
    for my $page ( @post_pages ) {
        for my $tag ( @{ $page-&gt;document-&gt;tags } ) {
            push @{ $tagged_docs{ $tag } }, $page;
        }
    }
    return %tagged_docs;
}

sub _tag_url {
    my ( $self, $tag ) = @_;
    $tag =~ s/\s+/-/g;
    return $tag;
}

=method recent_posts

    my @pages = $app-&gt;recent_posts( $count, %filter );

Get the last $count recent posts for this blog. Useful for templates and site
index pages.

%filter is an optional set of filters to apply to only show recent posts
matching the given criteria. The following filters are available:

=over 4

=item tags

(string) Only show posts with the given tag

=back

=cut

sub recent_posts {
    my ( $self, $count, %filter ) = @_;

    my $today = Time::Piece-&gt;new-&gt;ymd;
    my @pages;
    my @docs = $self-&gt;_sorted_docs;
    DOC: for my $doc ( @docs ) {
        QUERY: for my $attr ( keys %filter ) {
            my $value = $filter{ $attr };
            if ( $attr eq &#39;tags&#39; ) {
                next DOC unless grep { $_ eq $value } @{ $doc-&gt;tags };
            }
        }

        my $page = $self-&gt;_make_post_page( $doc );
        $page-&gt;path( join &quot;/&quot;, $self-&gt;url_root, $page-&gt;path );
        push @pages, $page;
        last if @pages &gt;= $count;
    }

    return @pages;
}

=method page_url

    my $url = $app-&gt;page_url( $page )

Return the absolute URL to this L&lt;page object|Statocles::Page&gt;, removing the
&quot;/index.html&quot; if necessary.

=cut

# XXX This is TERRIBLE. We need to do this better. Perhaps a &quot;url()&quot; helper in the
# template? And a full_url() helper? Or perhaps the template knows whether it should
# use absolute (/whatever) or full (http://www.example.com/whatever) URLs?

sub page_url {
    my ( $self, $page ) = @_;
    my $url = &quot;&quot;.$page-&gt;path;
    $url =~ s{/index[.]html$}{/};
    return $url;
}

1;
__END__

=head1 DESCRIPTION

This is a simple blog application for Statocles.

=head2 FEATURES

=over

=item *

Content dividers. By dividing your main content with &quot;---&quot;, you create
sections. Only the first section will show up on the index page or in RSS
feeds.

=item *

RSS and Atom syndication feeds.

=item *

Tags to organize blog posts. Tags have their own custom feeds so users can
subscribe to only those posts they care about.

=item *

Crosspost links to redirect users to a syndicated blog. Useful when you
participate in many blogs and want to drive traffic to them.

=item *

Post-dated blog posts to appear automatically when the date is passed. If a
blog post is set in the future, it will not be added to the site when running
C&lt;build&gt; or C&lt;deploy&gt;.

In order to ensure that post-dated blogs get added, you may want to run
C&lt;deploy&gt; in a nightly cron job.

=back

=head1 COMMANDS

=head2 post

    post [--date &lt;date&gt;] &lt;title&gt;

Create a new blog post, optionally setting an initial C&lt;title&gt;. The post will be
created in a directory according to the current date.

Initial post content can be read from C&lt;STDIN&gt;. This lets you write other programs
to generate content for blog posts (for example, to help automate release blog posts).

=head1 THEME

=over

=item blog =&gt; index

The index page template. Gets the following template variables:

=over

=item site

The L&lt;Statocles::Site&gt; object.

=item pages

An array reference containing all the blog post pages. Each page is a hash reference with the following keys:

=over

=item content

The post content

=item title

The post title

=item author

The post author

=back

=item blog =&gt; post

The main post page template. Gets the following template variables:

=over

=item site

The L&lt;Statocles::Site&gt; object

=item content

The post content

=item title

The post title

=item author

The post author

=back

=back

=back

=head1 SEE ALSO

=over 4

=item L&lt;Statocles::App&gt;

=back

</pre>

        </div>
        <footer>
            <script>
  (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
  })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

  ga('create', 'UA-61295159-3', 'auto');
  ga('send', 'pageview');

</script>

            <div class="container tagline">
                <a href="http://preaction.github.io/Statocles">Made with Statocles</a><br>
                <a href="http://www.perl.org">Powered by Perl</a>
            </div>
        </footer>
    </body>
</html>
