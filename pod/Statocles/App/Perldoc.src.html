<!DOCTYPE html>
<html>
    <head>
        <link href="/Statocles/theme/css/normalize.css" rel="stylesheet">
        <link href="/Statocles/theme/css/skeleton.css" rel="stylesheet">
        <link href="/Statocles/theme/css/statocles-default.css" rel="stylesheet">
        <title>Statocles</title>
        <link href="http://fonts.googleapis.com/css?family=Forum" rel="stylesheet" type="text/css">
<style>
    h1, h2, h3, h4, h5, h6 {
        font-family: 'Forum', sans-serif;
        font-weight: bold;
    }

    #index-banner {
        font-family: 'Forum', sans-serif;
        height: 300px;
        position: relative;
        margin-bottom: 1em;
        border-bottom: 1px solid #777
    }
    #index-banner:before, #index-banner:after {
        width: 60px;
        height: 300px;
        content: url(/Statocles/theme/images/column.png);
    }
    #index-banner:before {
        position: absolute;
        top: 0;
        left: 0;
    }
    #index-banner:after {
        position: absolute;
        top: 0;
        right: 0;
    }

    #index-banner h1 {
        padding-top: 1em;
        text-transform: uppercase;
        font-size: 72px;
        text-align: center;
    }
    #index-banner h1 small {
        display: block;
        font-size: 24px;
    }

    #index-banner .latest-release {
        text-align: center;
    }

</style>

    </head>
    <body>
        <header>
            <nav class="navbar">
                <div class="container">
                    <a class="brand" href="/Statocles/">Statocles</a>
                    <ul>
                        <li>
                            <a href="/Statocles/blog">Blog</a>
                        </li>
                        <li>
                            <a href="/Statocles/pod">Docs</a>
                        </li>
                        <li>
                            <a href="http://github.com/preaction/Statocles">Code</a>
                        </li>
                        <li>
                            <a href="http://github.com/preaction/Statocles/issues">Bugs</a>
                        </li>
                        <li>
                            <a href="http://metacpan.org/pod/Statocles">CPAN</a>
                        </li>
                        <li>
                            <a href="https://chat.mibbit.com/?channel=%23statocles&amp;server=irc.perl.org">IRC</a>
                        </li>
                    </ul>
                </div>
            </nav>
            <a href="https://github.com/preaction/Statocles"><img alt="Fork me on GitHub" data-canonical-src="https://s3.amazonaws.com/github/ribbons/forkme_right_red_aa0000.png" src="https://camo.githubusercontent.com/365986a132ccd6a44c23a9169022c0b5c890c387/68747470733a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f7265645f6161303030302e706e67" style="position: absolute; top: 0; right: 0; z-index: 1000; border: 0;"></a>

        </header>
        <div class="main container">
            <a href="/Statocles/pod/Statocles/App/Perldoc.html">Back to documentation</a>
<pre>package Statocles::App::Perldoc;
# ABSTRACT: Render documentation for Perl modules

use Statocles::Base &#39;Class&#39;;
use Statocles::Page::Plain;
use Scalar::Util qw( blessed );
use List::MoreUtils qw( any );
use Pod::Simple::Search;
use Pod::Simple::XHTML;
with &#39;Statocles::App&#39;;

=attr inc

The directories to search for modules. Defaults to @INC.

=cut

has inc =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; ArrayRef[Path],
    # We can&#39;t check for existence, because @INC might contain nonexistent
    # directories (I think)
    default =&gt; sub { [ @INC ] },
    coerce =&gt; sub {
        my ( $args ) = @_;
        return [ map { Path::Tiny-&gt;new( $_ ) } @$args ];
    },
);

=attr modules

The root modules to find. Required. All child modules will be included. Any module that does
not start with one of these strings will not be included.

=cut

has modules =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; ArrayRef[Str],
    required =&gt; 1,
);

=attr index_module

The module to use for the index page. Required.

=cut

has index_module =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; Str,
    required =&gt; 1,
);

=attr weave

If true, run the POD through L&lt;Pod::Weaver&gt; before converting to HTML

=cut

has weave =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; Bool,
    default =&gt; sub { 0 },
);

=attr weave_config

The path to the Pod::Weaver configuration file

=cut

has weave_config =&gt; (
    is =&gt; &#39;ro&#39;,
    isa =&gt; Path,
    default =&gt; sub { &#39;./weaver.ini&#39; },
    coerce =&gt; Path-&gt;coercion,
);

=method pages

Render the requested modules as HTML.

=cut

sub pages {
    my ( $self ) = @_;
    my @dirs = map { &quot;$_&quot; } @{ $self-&gt;inc };
    my $pod_base = &#39;https://metacpan.org/pod/&#39;;

    my %modules;
    for my $glob ( @{ $self-&gt;modules } ) {
        %modules = (
            %modules,
            %{ Pod::Simple::Search-&gt;new-&gt;inc(0)-&gt;limit_re( qr{^$glob} )-&gt;survey( @dirs ) },
        );

        # Also check for exact matches, for strange extensions
        for my $dir ( @dirs ) {
            my @glob_parts = split /::/, $glob;
            my $path = Path::Tiny-&gt;new( $dir, @glob_parts );
            if ( $path-&gt;is_file ) {
                $modules{ $glob } = &quot;$path&quot;;
            }
        }
    }


    #; use Data::Dumper;
    #; say Dumper \%modules;

    my @pages;
    for my $module ( keys %modules ) {

        my $path = $modules{ $module };
        #; use Data::Dumper;
        #; say Dumper $path;

        # Weave the POD before trying to make HTML
        my $pod = $self-&gt;weave
                ? $self-&gt;_weave_module( $path )
                : Path::Tiny-&gt;new( $path )-&gt;slurp
                ;

        my $parser = Pod::Simple::XHTML-&gt;new;
        $parser-&gt;perldoc_url_prefix( $pod_base );
        $parser-&gt;$_(&#39;&#39;) for qw( html_header html_footer );
        $parser-&gt;output_string( \(my $parser_output) );
        $parser-&gt;parse_string_document( $pod );
        #; say $parser_output;

        # Rewrite links for modules that we will be serving locally
        my $dom = Mojo::DOM-&gt;new( $parser_output );
        for my $node ( $dom-&gt;find( &#39;a[href]&#39; )-&gt;each ) {
            my $href = $node-&gt;attr( &#39;href&#39; );
            $href =~ s/$pod_base//;

            if ( grep { $href =~ /^$_/ } @{ $self-&gt;modules } ) {
                my $new_href;
                if ( $href eq $self-&gt;index_module ) {
                    $new_href = &#39;index.html&#39;;
                }
                else {
                    $new_href = join &#39;/&#39;, split /::/, $href;
                    $new_href .= &#39;.html&#39;;
                }
                $node-&gt;attr( href =&gt; $self-&gt;url( $new_href ) );
            }
        }

        my $source_path = &quot;$module.src.html&quot;;
        $source_path =~ s{::}{/}g;

        my ( @parts ) = split m{::}, $module;
        my @crumbtrail;
        for my $i ( 0..$#parts ) {
            my $trail_module = join &quot;::&quot;, @parts[0..$i];
            if ( $modules{ $trail_module } ) {
                push @crumbtrail, {
                    text =&gt; $parts[ $i ],
                    href =&gt; $self-&gt;url( $self-&gt;_module_href( $trail_module ) ),
                };
            }
            else {
                push @crumbtrail, {
                    text =&gt; $parts[ $i ],
                };
            }
        }

        my %page_args = (
            layout =&gt; $self-&gt;site-&gt;theme-&gt;template( site =&gt; &#39;layout.html&#39; ),
            template =&gt; $self-&gt;site-&gt;theme-&gt;template( perldoc =&gt; &#39;pod.html&#39; ),
            content =&gt; &quot;$dom&quot;,
            app =&gt; $self,
            path =&gt; $self-&gt;_module_href( $module ),
            data =&gt; {
                source_path =&gt; $self-&gt;url( $source_path ),
                crumbtrail =&gt; \@crumbtrail,
            },
        );

        if ( $module eq $self-&gt;index_module ) {
            unshift @pages, Statocles::Page::Plain-&gt;new( %page_args );
        }
        else {
            push @pages, Statocles::Page::Plain-&gt;new( %page_args );
        }

        # Add the source as a text file
        push @pages, Statocles::Page::Plain-&gt;new(
            path =&gt; $source_path,
            layout =&gt; $self-&gt;site-&gt;theme-&gt;template( site =&gt; &#39;layout.html&#39; ),
            template =&gt; $self-&gt;site-&gt;theme-&gt;template( perldoc =&gt; &#39;source.html&#39; ),
            content =&gt; Path::Tiny-&gt;new( $path )-&gt;slurp,
            app =&gt; $self,
            data =&gt; {
                doc_path =&gt; $self-&gt;url( $page_args{path} ),
                crumbtrail =&gt; \@crumbtrail,
            },
        );

    }

    return @pages;
}

sub _module_href {
    my ( $self, $module ) = @_;
    if ( $module eq $self-&gt;index_module ) {
        return join &#39;/&#39;, &#39;index.html&#39;;
    }

    my $page_url = &quot;$module.html&quot;;
    $page_url =~ s{::}{/}g;
    return $page_url;
}

=method _weave_module( $path )

Run Pod::Weaver on the POD in the given path

=cut

sub _weave_module {
    my ( $self, $path ) = @_;

    # Oh... My... GOD...
    my @missing;
    eval { require Pod::Weaver; 1; } or push @missing, &#39;Pod::Weaver&#39;;
    eval { require PPI; 1; } or push @missing, &#39;PPI&#39;;
    eval { require Pod::Elemental; 1; } or push @missing, &#39;Pod::Elemental&#39;;
    eval { require Encode; 1; } or push @missing, &#39;Encode&#39;;
    if ( @missing ) {
        die &quot;Cannot weave POD: Missing modules &quot; . join( &quot; &quot;, @missing );
    }

    my $perl_utf8 = Encode::encode( &#39;utf-8&#39;, Path::Tiny-&gt;new( $path )-&gt;slurp, Encode::FB_CROAK );
    my $ppi_document = PPI::Document-&gt;new( \$perl_utf8 ) or die PPI::Document-&gt;errstr;

    ### Copy/paste from Pod::Elemental::PerlMunger
    my $code_elems = $ppi_document-&gt;find(
        sub {
            return
                if grep { $_[ 1 ]-&gt;isa( &quot;PPI::Token::$_&quot; ) }
                qw(Comment Pod Whitespace Separator Data End);
            return 1;
        }
    );

    $code_elems ||= [];
    my @pod_tokens;

    my @queue = $ppi_document-&gt;children;
    while ( my $element = shift @queue ) {
        if ( $element-&gt;isa( &#39;PPI::Token::Pod&#39; ) ) {
            # save the text for use in building the Pod-only document
            push @pod_tokens, &quot;$element&quot;;
        }

        if ( blessed $element &amp;&amp; $element-&gt;isa( &#39;PPI::Node&#39; ) ) {
            # Depth-first keeps the queue size down
            unshift @queue, $element-&gt;children;
        }
    }

    ## Check for any problems, like POD inside of heredoc or strings
    my $finder = sub {
        my $node = $_[ 1 ];
        return 0
            unless any { $node-&gt;isa( $_ ) }
        qw( PPI::Token::Quote PPI::Token::QuoteLike PPI::Token::HereDoc );
        return 1 if $node-&gt;content =~ /^=[a-z]/m;
        return 0;
    };

    if ( $ppi_document-&gt;find_first( $finder ) ) {
        warn &quot;can&#39;t invoke Pod::Weaver on &#39;$path&#39;: There is POD in string literals&quot;;
        return &#39;&#39;;
    }

    my $pod_str = join &quot;\n&quot;, @pod_tokens;
    my $pod_document = Pod::Elemental-&gt;read_string( $pod_str );

    ### MUNGE THE POD HERE!

    my $weaver = Pod::Weaver-&gt;new_from_config(
        { root =&gt; $self-&gt;weave_config-&gt;parent-&gt;stringify },
    );
    my $weaved_doc = $weaver-&gt;weave_document({
        pod_document =&gt; $pod_document,
        ppi_document =&gt; $ppi_document,
    });

    ### END MUNGE THE POD

    my $pod_text = $weaved_doc-&gt;as_pod_string;

    #; say $pod_text;
    return $pod_text;
}

1;
__END__

=head1 DESCRIPTION

This application generates HTML from the POD in the requested modules.

=cut

</pre>

        </div>
        <footer>
            <script>
  (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
  })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

  ga('create', 'UA-61295159-3', 'auto');
  ga('send', 'pageview');

</script>

            <div class="container tagline">
                <a href="http://preaction.github.io/Statocles">Made with Statocles</a><br>
                <a href="http://www.perl.org">Powered by Perl</a>
            </div>
        </footer>
    </body>
</html>
